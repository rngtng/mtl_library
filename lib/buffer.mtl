//###############################################
#include ./lib/integer.mtl
//---------------------- Buffer -------------------------------

// #### LEDS
//  Buffers 0 - 9, where 0-4 are used for onetime, and 5-9 for loop playback.

// #### EARS
// Buffers 10 - 13, where 10 & 11 are used for onetime, and 12 & 13 for loop playback.

var buffers;;
type BUFFER=[valB stepB lenB posB loopB];;

fun buffersInit len=
  set buffers = tabnew nil len;
  for index = 0; index < len; index + 1 do
    set buffers.index = [valB:""]
;;

fun _bufferFill index step loop val=
  let strlen val -> len in
    set buffers.index = [valB:val stepB:step lenB:len posB:0 loopB:loop]
;;

fun bufferFill index val=
  _bufferFill index 1 0 val
;;

fun bufferFill3 index val=
  _bufferFill index 3 0 val
;;

fun loopBufferFill index val=
  _bufferFill index 1 1 val
;;

fun loopBufferFill3 index val=
  _bufferFill index 3 1 val
;;

fun buffersFill from to val=
  for index = from; index <= to; index + 1 do
    bufferFill index val
;;

fun buffersFill3 from to val=
  for index = from; index <= to; index + 1 do
    bufferFill3 index val
;;

fun bufferRead index=
  let buffers.index -> buffer in
  (
    if buffer.posB < buffer.lenB then
    (
      set buffer.posB = buffer.posB + buffer.stepB;
      let buffer.posB - buffer.stepB -> pos in
      (
        if buffer.stepB == 3 then
          CtoI3 buffer.valB pos
        else
          strget buffer.valB pos
      )
    )
    else
      if buffer.loopB > 0 && buffer.posB > 0 then
      (
        set buffer.posB = 0;
        bufferRead index
      )
      else
        nil
  )
;;
